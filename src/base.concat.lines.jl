function concat_lines(
        paths::Vector{<:AbstractString};
        cfg = SourceConcat.load_config(), 
        sortpaths = true
    )

    if sortpaths 
        paths = sort(paths)
    end

    # --- Read config values with defaults ---
    heading_level = get(cfg, "separator.heading.level", 2)
    missing_mode  = get(cfg, "missing", "error")

    # --- relnames prefix ---
    prefix = common_prefix(paths)
    relnames = Dict{String, String}()
    for path in paths
        relname = replace(path, prefix => "")
        relname = strip(relname, '/')
        relnames[path] = relname
    end
    
    # --- Globals ---
    concat_lines = String[]
    heading = repeat("#", heading_level)

    # --- Prepare output file ---
    timestamp = Dates.format(now(), dateformat"yyyy-mm-dd HH:MM:SS")
    push!(concat_lines, "<!-- Generated by SourceConcat.jl at $timestamp -->")
    push!(concat_lines, "")
    

    # --- Table of content ---
    push!(concat_lines, "***")
    push!(concat_lines, "$heading TABLE OF CONTENTS")
    push!(concat_lines, "")
    for (pi, path) in enumerate(paths)
        relname = relnames[path]
        push!(concat_lines, "$(pi). [Content of '$(relname)'](#sc-$(pi))")
    end
    
    # --- Add files ---
    for (pi, path) in enumerate(paths)
        if !isfile(path)
            msg = "File not found: $path"
            if missing_mode == "warn_skip"
                @warn msg
                continue
            else
                error(msg)
            end
        end

        # --- Header separator ---
        push!(concat_lines, "\n")
        push!(concat_lines, "***")
        relname = relnames[path]
        push!(concat_lines, "<!-- ANCHOR: sc-$(pi) -->")
        push!(concat_lines, "$heading $pi ⟦$(relname)⟧")
        push!(concat_lines, "")
        
        
        # --- Append file content ---
        push!(concat_lines, "<!-- START CONTENT: SRC: path=\"$relname\" -->")
        try
            lang = _getlang(path)
            push!(concat_lines, "`````````$(lang)")
            for line in eachline(path; keep=false)
                push!(concat_lines, line)
            end
            push!(concat_lines, "`````````")
        catch e
            @warn "Error reading $relname: $e"
        end
        push!(concat_lines, "<!-- END CONTENT: SRC: path=\"$relname\" -->")

        # --- Blank lines between sections ---
        push!(concat_lines, "\n")
        
    end
    
    
    # open(output_path, "w") do io
    # end

    # println("✅ Concatenated $(length(paths)) files → $output_path")
    return concat_lines
end
